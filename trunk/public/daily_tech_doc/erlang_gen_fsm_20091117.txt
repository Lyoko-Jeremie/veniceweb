总结Erlang OTP中的gen_fsm -> finite state machine

1. 有限状态机的描述:
State(S) x Event(E) -> Action(A),
                       State(S')
(其中State包含: StateName和StateData)
当我们处于状态S的时候，发生了事件E，我们需要执行动作A，并妆变为状态S'
(要注意的是S和S'可能是同一状态)

在gen_fsm中的状态规则转换约定是:
StateName(Event, StateData) ->
  ... 执行动作的代码 ...
  {next_state, StateName', StateData'} %% 转换为新的状态.

2. 一个密码锁的例子:
[test.erl]
-module(test).
-export([start_link/1, button/1]).
-export([init/1, locked/2, open/2]).

start_link(Code) ->
    gen_fsm:start_link({local, ?MODULE}, ?MODULE, Code, []). %% Code是init参数

button(Digit) ->
    gen_fsm:send_event(?MODULE, {button, Digit}).

init(Code) ->
    SoFar = [],
    {ok, locked, {SoFar, lists:reverse(Code)}}.  %% {ok, StateName, StateData}

locked({button, Digit}, {SoFar, Code}) ->
    case [Digit|SoFar] of
	Code ->
            io:format("you have open the door, the door will close again after 30 seconds~n", []),
	    {next_state, open, {[], Code}, 30000};  %% {next_state, StateName, StateData, Timeout}
                                                    %% 30秒以后会触发 StateName(timeout, StateData)
	Incomplete when length(Incomplete) < length(Code) ->
	    {next_state, locked, {Incomplete, Code}};
	_Wrong ->
	    io:format("you inputed the wrong code, go to the init state again~n", []),
	    {next_state, locked, {[], Code}} %% {next_state, StateName, StateData}
    end.

open(timeout, StateData) -> 
    io:format("the door closed again~n.", []),
    {next_state, locked, StateData}.
 
 

测试:
test:start_link("mycode"). %% 输入mycode作为安全门的初始密码
{ok,<0.66.0>}
test:button($m). %% 输入正确密码开锁: [$m, $y, $c, $o, $d, $e]作为密码开锁
ok
test:button($y).
ok
test:button($c).
ok
test:button($o).
ok
test:button($d).
ok
test:button($e).
you have open the door, the door will close again after 30 seconds
ok
the door closed again  %% 门打开30秒后自动关闭
test:button($m).       %% 输入错误密码开锁: [$m, $y, $c, $a, $d, $e]作为密码开锁
ok
test:button($y).
ok
test:button($c).
ok
test:button($a).
ok
test:button($d).
ok
test:button($e).
you inputed the wrong code, go to the init state again
ok

