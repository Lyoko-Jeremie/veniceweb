-module(erails_controller_server).
-behaviour(gen_server).

%% Name-controller maps
%%
%% "login" -> login_controller
%% "logout" -> logout_controller
%% 
%% This prevents creating an atom() for every incoming
%% request which could lead to a potential DoS attack by filling the global
%% atom() table.

%% API
-export([start/1, get_controller/1, list_controller/0]).

%% gen_server callbacks
-export([init/1, handle_call/3, handle_cast/2, handle_info/2,
	 terminate/2, code_change/3]).

%% 
%% @doc Start the app with the Basedir of the application.
%% Basedir is determined in the supervisor
%%
start(BaseDir) ->
    gen_server:start_link({local, ?MODULE}, ?MODULE, BaseDir,[]).

%%
%% @doc Given the name of the controller in the URL
%% return the module name in the mapping
%%
get_controller(Controller) ->
    gen_server:call(?MODULE, {get_controller, Controller}).

%%
%% @doc Simple helper to list the name-controller mapping
%%
list_controller() ->
    gen_server:call(?MODULE, view).

%% @hidden
init(BaseDir) ->
    {ok, load_controllers(BaseDir)}.

%% ---------Callbacks------------------------------

%% @hidden
handle_call({get_controller, Controller},_From, State) ->
    Reply = case lists:keysearch(Controller,1,State) of
		{value,{_,C}} ->
		    {ok,C};
		false ->
		    no_controller
	    end,
    {reply, Reply, State};

%% @hidden
handle_call(view,_From,State) ->
    error_logger:info_msg("Controller Map:~n~p~n",[State]),
    {reply,ok,State}.

%% @hidden
handle_cast(_Msg, State) ->
    {noreply, State}.
%% @hidden
handle_info(_Info, State) ->
    {noreply, State}.
%% @hidden
terminate(_Reason, _State) ->
    ok.
%% @hidden
code_change(_OldVsn, State, _Extra) ->
    {ok, State}.

%%--------------------------------------------------------------------
%%% Internal functions
%%--------------------------------------------------------------------
list_controllers(BaseDir) ->
    Path = filename:join([BaseDir, "src", "*_controller.erl"]),
    %% returns a list of all files that match Unix-style wildcard-string Wildcard.
    filelib:wildcard(Path).

%% @return [{"login", login_controller}, {"logout", logout_controller}]    
load_controllers(BaseDir) ->
    lists:foldl(fun(File,Acc) ->
			OrgName = filename:basename(File, ".erl"),
			KeyName = string:substr(OrgName, 1, length(OrgName) - length("_controller")), 
			AtomName = list_to_atom(OrgName),
			[{KeyName,AtomName}|Acc]
		end,
		[],
		list_controllers(BaseDir)).
    
